buildscript {
    ext.kotlinVersion = '1.3.50'
    ext.dokkaVersion = "0.10.0"

    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'signing'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

group 'com.control-j'
version '1.0.2-SNAPSHOT'
archivesBaseName = 'komodo'

sourceCompatibility = 1.8

task javadocJar(type: Jar) {
    from dokka
    archiveClassifier = 'javadoc'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

repositories {
    mavenCentral()
    jcenter()
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

shadowJar {
    classifier = null
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }

    publications {
        shadow(MavenPublication) { publication ->
            artifact shadowJar
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = 'Komodo'
                packaging 'jar'
                description = 'A Kotlin noSQL database with MVStore as the backend'
                url = 'https://github.com/clydebarrow/komodo'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'clyde'
                        name = 'Clyde Stubbs'
                        url = 'https://github.com/clydebarrow'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/clydebarrow/komodo.git'
                    developerConnection = 'scm:git:https://github.com/clydebarrow/komodo.git'
                    url = 'https://github.com/clydebarrow/komodo'
                }
            }
        }
    }
}

signing {
    sign configurations.archives
    sign publishing.publications.shadow
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly "org.threeten:threetenbp:1.4.0"
    compileOnly "io.reactivex.rxjava2:rxjava:2.2.11"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    //noinspection DifferentStdlibGradleVersion
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
