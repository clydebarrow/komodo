buildscript {
    ext.kotlinVersion = '1.3.50'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'signing'
apply plugin: 'maven-publish'


group 'com.control-j'
version '1.0.0-SNAPSHOT'
archivesBaseName = 'komodo'


sourceCompatibility = 1.8

idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

repositories {
    mavenCentral()
}

javadoc {
	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.author = true
	options.header = project.name
	options.use = true
    options.encoding("UTF-8")
	options.addStringOption("Xdoclint:none", "-quiet")
    failOnError = false
 }


sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }

    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

signing {
    sign configurations.archives
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Komodo'
                description = 'A Kotlin noSQL database with MVStore as the backend'
                url = 'https://github.com/clydebarrow/komodo'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'clyde'
                        name = 'Clyde Stubbs'
                        url = 'https://github.com/clydebarrow'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/clydebarrow/komodo.git'
                    developerConnection = 'scm:git:https://github.com/clydebarrow/komodo.git'
                    url = 'https://github.com/clydebarrow/komodo'
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username ossrhUser
                password ossrhPassword
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly "org.threeten:threetenbp:1.4.0"
    compileOnly "io.reactivex.rxjava2:rxjava:2.2.11"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    //noinspection DifferentStdlibGradleVersion
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
